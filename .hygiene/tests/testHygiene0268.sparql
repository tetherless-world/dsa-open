prefix fibo-fnd-rel-rel: <https://spec.edmcouncil.org/fibo/ontology/FND/Relations/Relations/>
prefix fibo-der-rtd-irswp: <https://spec.edmcouncil.org/fibo/DER/RateDerivatives/IRSwaps/>
prefix fibo-der-drc-swp: <https://spec.edmcouncil.org/fibo/DER/DerivativesContracts/Swaps/>
prefix fibo-ind-ir-ir: <https://spec.edmcouncil.org/fibo/IND/InterestRates/InterestRates/>
prefix ex:    <http://www.example.org/time#>
prefix owl:   <http://www.w3.org/2002/07/owl#>
prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix spin:  <http://spinrdf.org/spin#>
prefix xsd:   <http://www.w3.org/2001/XMLSchema#>
prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
prefix sp:    <http://spinrdf.org/sp#>
prefix fibo-test-lattice: <http://www.omg.org/spec/fibo/etc/testing/patterns/lattice#>
prefix fibo-fnd-aap-agt: <https://spec.edmcouncil.org/fibo/FND/AgentsAndPeople/Agents/>
prefix fibo-fnd-pty-rl: <https://spec.edmcouncil.org/fibo/FND/Parties/Roles/>
prefix afn: <http://jena.apache.org/ARQ/function#>
prefix fibo-fnd-utl-av: <https://spec.edmcouncil.org/fibo/ontology/FND/Utilities/AnnotationVocabulary/>
prefix sm: <http://www.omg.org/techprocess/ab/SpecificationMetadata/>
prefix dct: <http://purl.org/dc/terms/>

##
# banner We should not make explicit references to owl:Thing

SELECT DISTINCT ?error
WHERE {
 {
  {?s ?p owl:Thing .}
  UNION
  {?s rdfs:subClassOf|owl:equivalentClass [?p owl:Thing]}
  }
  FILTER (REGEX (xsd:string (?s), "twc"))
  FILTER (?p != owl:someValuesFrom)
  BIND (afn:localname (?p) AS ?prop)
  BIND (concat ("PRODERROR:", xsd:string (?s), " has an explicit reference to owl:Thing (", ?prop, ").")
	  AS ?error)
 }
